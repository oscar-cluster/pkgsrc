# -*- shell-script -*-
#
# Copyright (c) 2002-2004 The Trustees of Indiana University.  
#                         All rights reserved.
#
# This file is part of the Env-switcher software package.  For license
# information, see the LICENSE file in the top-level directory of the
# Env-switcher source distribution.
#
# $Id: configure.in,v 1.6 2004/03/07 22:06:54 jsquyres Exp $
#

# Init autoconf

AC_INIT(./src/switcher.in)
AC_CONFIG_AUX_DIR(./dist)

show_title() {
  cat <<EOF

*** $1
EOF
}

show_title "Initialization, setup"

# Init automake
# Get the version of LAM that we are installing

get_version="sh $srcdir/dist/get_switcher_version $srcdir"
SWITCHER_VERSION="`eval $get_version --full`"
MAJOR_VERSION="`eval $get_version --major`"
MINOR_VERSION="`eval $get_version --minor`"
RELEASE_VERSION="`eval $get_version --release`"
ALPHA_VERSION="`eval $get_version --alpha`"
BETA_VERSION="`eval $get_version --beta`"
AC_DEFINE_UNQUOTED(MAJOR_VERSION, $MAJOR_VERSION)
AC_DEFINE_UNQUOTED(MINOR_VERSION, $MINOR_VERSION)
AC_DEFINE_UNQUOTED(RELEASE_VERSION, $RELEASE_VERSION)
AC_DEFINE_UNQUOTED(ALPHA_VERSION, $ALPHA_VERSION)
AC_DEFINE_UNQUOTED(BETA_VERSION, $BETA_VERSION)
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")

# Need to also AC_SUBST these for share/include/patchlevel.h and
# share/include/mpif.h

AC_SUBST(MAJOR_VERSION)
AC_SUBST(MINOR_VERSION)
AC_SUBST(RELEASE_VERSION)
AC_SUBST(ALPHA_VERSION)
AC_SUBST(BETA_VERSION)
AC_SUBST(SWITCHER_VERSION)

echo "Configuring env-switcher version $SWITCHER_VERSION"

#
# The third argument to AM_INIT_AUTOMAKE surpresses the PACKAGE and
# VERSION macors
#

AM_INIT_AUTOMAKE(env-switcher, $SWITCHER_VERSION, 'no')

#
# Make automake clean emacs ~ files for "make clean"
#

CLEANFILES="*~ .*~"
AC_SUBST(CLEANFILES)

#
# eval down to absolute directories (vs., "${prefix}/etc")
#

save="$exec_prefix"
if test "$exec_prefix" = "NONE"; then
    exec_prefix="$prefix"
fi

SWITCHER_PREFIX="`eval echo $prefix`"
AC_SUBST(SWITCHER_PREFIX)
SWITCHER_BINDIR="`eval echo $bindir`"
AC_SUBST(SWITCHER_BINDIR)
SWITCHER_SYSCONFDIR="`eval echo $sysconfdir`"
AC_SUBST(SWITCHER_SYSCONFDIR)
SWITCHER_PKGDATADIR="`eval echo $datadir/env-switcher`"
AC_SUBST(SWITCHER_PKGDATADIR)
SWITCHER_MANDIR="`eval echo $mandir`"
AC_SUBST(SWITCHER_MANDIR)

exec_prefix="$save"
unset save

#
# Do we want silent output by default?
#

AC_MSG_CHECKING([if suppress errors by default])
AC_ARG_ENABLE(suppress-errors,
    AC_HELP_STRING([--enable-suppress-errors],
        [Set switcher to default to not show any errors]))
if test "$enable_suppress_errors" = "yes"; then
    AC_MSG_RESULT([yes])
    WANT_SILENT=1
    DEFAULT_ANNOUNCE_VALUE="none"
else
    AC_MSG_RESULT([no])
    WANT_SILENT=0
    DEFAULT_ANNOUNCE_VALUE="warn,error"
fi
AC_SUBST(DEFAULT_ANNOUNCE_VALUE)

#
# Figure out where modulefiles get installed
#

AC_MSG_CHECKING([where modulefiles get installed])
AC_ARG_WITH(modulefiles, 
    AC_HELP_STRING([--with-modulefiles=DIR],
        [directory where modulefiles get installed]))

MODULEFILESDIR=
if test "$with_modulefiles" != ""; then
    MODULEFILESDIR="$with_modulefiles"
else
    MODULEFILESDIR="/opt/modules/oscar-modulefiles"
fi

AC_MSG_RESULT([$MODULEFILESDIR])
AC_SUBST(MODULEFILESDIR)

#
# All done
#

AC_OUTPUT([
    Makefile

    dist/Makefile

    src/Makefile
    src/switcher
    src/switcher.tcl

    man/Makefile
], [chmod +x src/switcher])
